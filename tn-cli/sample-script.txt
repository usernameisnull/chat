#  This script creates a new user Test User, uses fnd to locate user Alice,
# subscribes Test User to Alice, sends typing notification, sends a message,
# deletes user Test.

# Create user 'Test User'

.must $user acc --scheme=basic --secret=test:test123 \
  --fn='Test User' --photo=./test-128.jpg --tags=test,test-user --do-login \
  --auth=JRWPA --anon=JW \
  --cred=email:test@example.com

# Print out user's auth token.
# Params is a map, thus must be addressed as params[x] instead of params.x
.log $user.params[token]

# Login and confirm credentials with a dummy response
.must login --scheme=token --secret=$user.params[token] --cred=email::123456

# Alternatively just login with an existing user.
# .must $user login bob:bob123

# Find Alice
.must sub fnd
.must set fnd --public=email:alice@example.com
.must $meta get fnd --sub

# Print out Alice's UID.
.log $meta.sub[0].user_id

# Subscribe to Alice
.must sub $meta.sub[0].user_id

# Send typing notification to Alice (async)
note $meta.sub[0].user_id

# Send message to Alice (async)
pub $meta.sub[0].user_id 'Hello, Alice'

# Wait 2 seconds before cleaning up.
.sleep 2000

# Clean up: delete Test User
.await del user --hard

# Wait for completion before exiting.
.sleep 2000
